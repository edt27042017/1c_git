
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДокументРезультат.Очистить();
	ДокументРезультат.Автомасштаб = Истина;
	МакетПланФакты = ПолучитьМакет("ПланФакты");
	
	//
	
	
	//Выведем заголовок отчета
	
	ПредставлениеОбъектаУправления = KPI_ОбщегоНазначенияСервер.ПолучитьПредставлениеПоСтруктуреКомпанииНаДату(ОбъектУправления,Период);
	
	ЗаголовокОтчета = СтрЗаменить(ЗаголовокОтчета, "%ПредставлениеПодразделения%",ПредставлениеОбъектаУправления);
	ПредставлениеПериода = KPI_ИнтерфейсВызовСервера.ПредставлениеПериодаПланирования(Период,ПериодичностьПланирования);
	ЗаголовокОтчета = СтрЗаменить(ЗаголовокОтчета, "%Период%", ПредставлениеПериода);
	
	ОбластьЗаголовок = МакетПланФакты.ПолучитьОбласть("ЗаголовокЦелевыеПоказатели");
	ОбластьЗаголовок.Параметры.ОбъектУправления = ПредставлениеОбъектаУправления;
	ОбластьЗаголовок.Параметры.представлениеПериода = ПредставлениеПериода;
	
	ДокументРезультат.Вывести(ОбластьЗаголовок);
	ГраницыПериода = KPI_ОбщегоНазначенияСервер.ГраницыПериода(Период,ПериодичностьПланирования);
	//
	
	//Выведем целевые показатели
	
	ПланФакты = KPI_ОбщегоНазначенияСервер.ПолучитьПланыИФакты(Период,ГраницыПериода.НачалоПериода,
	ГраницыПериода.КонецПериода,ПериодичностьПланирования,ОбъектУправления,,Истина);
	
	ОбластьЗаголовокГруппировки = МакетПланФакты.ПолучитьОбласть("ГрупировкаЦелевыеПоказатели");
	ДокументРезультат.Вывести(ОбластьЗаголовокГруппировки);
	
	ОбластьДетальныеЗаписи = МакетПланФакты.ПолучитьОбласть("ДетальльныеЗаписиЦелевыеПоказатели");
	Для Каждого Стр из ПланФакты Цикл
		ЗаполнитьЗначенияСвойств(ОбластьДетальныеЗаписи.Параметры,Стр);
		ЗаполнитьЗначенияСвойств(ОбластьДетальныеЗаписи.Параметры,Стр.ЦелевойПоказатель);
		ДокументРезультат.Вывести(ОбластьДетальныеЗаписи);
	КонецЦикла;
	
	ОбластьПодвалПоказателей = МакетПланФакты.ПолучитьОбласть("ПодвалЦелевыеПоказатели");
	ОбластьПодвалПоказателей.Параметры.КПР = ПланФакты.Итог("КПР");
	ОбластьПодвалПоказателей.Параметры.Вес = ПланФакты.Итог("Вес");
	ДокументРезультат.Вывести(ОбластьПодвалПоказателей);
	
	//Выведем смарт задачи
	
	ОбластьЗаголовок = МакетПланФакты.ПолучитьОбласть("ЗаголовокСмартЗадачи");
	ДокументРезультат.Вывести(ОбластьЗаголовок);
	
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	KPI_Планы.План,
	               |	ВЫРАЗИТЬ(KPI_Планы.Регистратор КАК Документ.KPI_СмартЗадача) КАК Регистратор
	               |ПОМЕСТИТЬ СмартЗадачиПланы
	               |ИЗ
	               |	РегистрНакопления.KPI_Планы КАК KPI_Планы
	               |ГДЕ
	               |	KPI_Планы.Регистратор ССЫЛКА Документ.KPI_СмартЗадача
	               |	И KPI_Планы.СтруктураКомпании = &ОбъектУправления
	               |	И KPI_Планы.ПериодичностьПланирования = &ПериодичностьПланирования
	               |	И KPI_Планы.Регистратор.ВыполнитьДо МЕЖДУ &НачалоПериода И &КонецПериода
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Регистратор
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВЫРАЗИТЬ(KPI_Факты.Регистратор КАК Документ.KPI_СмартЗадача) КАК Регистратор,
	               |	KPI_Факты.Факт,
	               |	KPI_Факты.СтруктураКомпании
	               |ПОМЕСТИТЬ ФактыСмартЗадач
	               |ИЗ
	               |	РегистрНакопления.KPI_Факты КАК KPI_Факты
	               |ГДЕ
	               |	KPI_Факты.Регистратор ССЫЛКА Документ.KPI_СмартЗадача
	               |	И KPI_Факты.СтруктураКомпании = &ОбъектУправления
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Регистратор
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СмартЗадачиПланы.План КАК План,
	               |	ЕСТЬNULL(ФактыСмартЗадач.Факт, 0) КАК Факт,
	               |	ЕСТЬNULL(ФактыСмартЗадач.Факт, 0) / СмартЗадачиПланы.План * 100 КАК ПроцентВыполнения,
	               |	СмартЗадачиПланы.Регистратор.ВыполнитьДо КАК СрокИсполнения,
	               |	СмартЗадачиПланы.Регистратор.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	СмартЗадачиПланы.Регистратор.НаименованиеЗадачи КАК СмартЗадача,
	               |	СмартЗадачиПланы.Регистратор КАК Регистратор
	               |ИЗ
	               |	СмартЗадачиПланы КАК СмартЗадачиПланы
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ФактыСмартЗадач КАК ФактыСмартЗадач
	               |		ПО СмартЗадачиПланы.Регистратор = ФактыСмартЗадач.Регистратор";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ОбъектУправления"          , ОбъектУправления);
	Запрос.УстановитьПараметр("ПериодичностьПланирования" , ПериодичностьПланирования);
	Запрос.УстановитьПараметр("НачалоПериода"             , ГраницыПериода.НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода"              , ГраницыПериода.КонецПериода);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ОбластьГруппировкаЗадач = МакетПланФакты.ПолучитьОбласть("ГрупировкаСмартЗадачи");
	ДетальныеЗаписиЗадач    = МакетПланФакты.ПолучитьОбласть("ДетальльныеЗаписиСмартЗадачи");
	ДокументРезультат.Вывести(ОбластьГруппировкаЗадач);
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ДетальныеЗаписиЗадач.Параметры,Выборка);
		ДокументРезультат.Вывести(ДетальныеЗаписиЗадач);
	КонецЦикла;
		
	//Стандарты
	ОбластьЗаголовок = МакетПланФакты.ПолучитьОбласть("ЗаголовокСтандарт");
	ДокументРезультат.Вывести(ОбластьЗаголовок);
	//
	
	ТекстЗапроса = "ВЫБРАТЬ
	|	KPI_СтруктураКомпанииПринятые.Период КАК Принят,
	|	KPI_СтруктураКомпанииПринятые.Сотрудник КАК Сотрудник,
	|	KPI_СтруктураКомпанииПринятые.СтруктураКомпании КАК СтруктураКомпании
	|ПОМЕСТИТЬ Принятые
	|ИЗ
	|	РегистрСведений.KPI_СтруктураКомпании КАК KPI_СтруктураКомпанииПринятые
	|ГДЕ
	|	НЕ KPI_СтруктураКомпанииПринятые.Уволен
	|	И НЕ KPI_СтруктураКомпанииПринятые.СтруктураУдалена
	|	И НЕ KPI_СтруктураКомпанииПринятые.Сотрудник = ЗНАЧЕНИЕ(Справочник.KPI_Сотрудники.ПустаяСсылка)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СтруктураКомпании,
	|	Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	KPI_СтруктураКомпанииУволеные.СтруктураКомпании КАК СтруктураКомпании,
	|	KPI_СтруктураКомпанииУволеные.Сотрудник КАК Сотрудник,
	|	KPI_СтруктураКомпанииУволеные.Период КАК Уволен
	|ПОМЕСТИТЬ Уволеные
	|ИЗ
	|	РегистрСведений.KPI_СтруктураКомпании КАК KPI_СтруктураКомпанииУволеные
	|ГДЕ
	|	KPI_СтруктураКомпанииУволеные.Уволен
	|	И НЕ KPI_СтруктураКомпанииУволеные.СтруктураУдалена
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СтруктураКомпании,
	|	Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Принятые.Принят,
	|	ЕСТЬNULL(Уволеные.Уволен, ДАТАВРЕМЯ(2999, 12, 31)) КАК Уволен,
	|	Принятые.СтруктураКомпании,
	|	Принятые.Сотрудник
	|ПОМЕСТИТЬ ПериодыРаботыСотрудников
	|ИЗ
	|	Принятые КАК Принятые
	|		ЛЕВОЕ СОЕДИНЕНИЕ Уволеные КАК Уволеные
	|		ПО Принятые.СтруктураКомпании = Уволеные.СтруктураКомпании
	|			И Принятые.Сотрудник = Уволеные.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ Принятые
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ Уволеные
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	KPI_СтандартыСрезПоследних.Стандарт КАК Стандарт,
	|	KPI_СтандартыСрезПоследних.ДатаОценки КАК ДатаОценки,
	|	KPI_СтандартыСрезПоследних.Ответственный КАК Ответственный,
	|	KPI_СтандартыСрезПоследних.Основание,
	|	KPI_СтандартыСрезПоследних.ВесСтандарта КАК ВесСтандарта,
	|	KPI_СтандартыСрезПоследних.Оцениваемый КАК Оцениваемый,
	|	KPI_СтандартыСрезПоследних.ВесОтветственного КАК ВесОценкиОтветственного
	|ПОМЕСТИТЬ СтандартыПлан
	|ИЗ
	|	РегистрСведений.KPI_Стандарты.СрезПоследних(
	|			&КонецПериода,
	|			Основание ССЫЛКА Документ.KPI_Планирование
	|				И (ДатаОценки МЕЖДУ &НачалоПериода И &КонецПериода)
	|				И Оцениваемый = &ОбъектУправления
	|				И ПериодичностьПланирования = &ПериодичностьПланирования) КАК KPI_СтандартыСрезПоследних
	|
	|СГРУППИРОВАТЬ ПО
	|	KPI_СтандартыСрезПоследних.Ответственный,
	|	KPI_СтандартыСрезПоследних.Стандарт,
	|	KPI_СтандартыСрезПоследних.ДатаОценки,
	|	KPI_СтандартыСрезПоследних.Основание,
	|	KPI_СтандартыСрезПоследних.ВесСтандарта,
	|	KPI_СтандартыСрезПоследних.Оцениваемый,
	|	KPI_СтандартыСрезПоследних.ВесОтветственного
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	KPI_Факты.КлючАналитики.Аналитика1 КАК Стандарт,
	|	KPI_Факты.КлючАналитики.Аналитика2 КАК ПереодичностьПланирования,
	|	KPI_Факты.КлючАналитики.Аналитика3 КАК Ответственный,
	|	KPI_Факты.КлючАналитики.Аналитика4 КАК Оцениваемый,
	|	KPI_Факты.КлючАналитики.Аналитика5 КАК ДатаОценки,
	|	KPI_Факты.Факт КАК Факт,
	|	KPI_Факты.ОценкаСтандарта,
	|	KPI_Факты.Регистратор,
	|	KPI_Факты.Комментарий
	|ПОМЕСТИТЬ СтандартыФакт
	|ИЗ
	|	РегистрНакопления.KPI_Факты КАК KPI_Факты
	|ГДЕ
	|	KPI_Факты.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И KPI_Факты.Регистратор ССЫЛКА Документ.KPI_ВводОценокСтандартов
	|	И KPI_Факты.ЦелевойПоказатель = ЗНАЧЕНИЕ(Справочник.KPI_ЦелевыеПоказатели.Стандарт)
	|	И KPI_Факты.КлючАналитики.Аналитика4 = &ОбъектУправления
	|	И KPI_Факты.КлючАналитики.Аналитика2 = &ПериодичностьПланирования
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтандартыПлан.Стандарт КАК Стандарт,
	|	СтандартыПлан.Ответственный КАК Ответственный,
	|	СтандартыПлан.ВесСтандарта,
	|	ЕСТЬNULL(СтандартыФакт.Факт, 0) КАК Факт,
	|	СтандартыПлан.ВесОценкиОтветственного,
	|	СтандартыФакт.ОценкаСтандарта,
	|	СтандартыПлан.Основание,
	|	СтандартыФакт.Регистратор КАК ДокументВводОценокСтандартов,
	|	СтандартыПлан.ДатаОценки,
	|	СтандартыФакт.Комментарий
	|ПОМЕСТИТЬ ВТ
	|ИЗ
	|	СтандартыПлан КАК СтандартыПлан
	|		ЛЕВОЕ СОЕДИНЕНИЕ СтандартыФакт КАК СтандартыФакт
	|		ПО СтандартыПлан.Стандарт = СтандартыФакт.Стандарт
	|			И СтандартыПлан.Ответственный = СтандартыФакт.Ответственный
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ответственный
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Самооценка.Стандарт,
	|	Самооценка.ВесСтандарта,
	|	Самооценка.ОценкаСтандарта КАК Самооценка,
	|	Самооценка.Комментарий КАК КомментарийСамооценки,
	|	ОценкиОтветсвеных.ВесОценкиОтветственного КАК ВесОценкиОтветственного,
	|	ОценкиОтветсвеных.ОценкаСтандарта КАК ОценкаОтвественного,
	|	ОценкиОтветсвеных.Комментарий КАК КомментарийОтветсвенного,
	|	ОценкиОтветсвеных.Ответственный КАК ОтветственныйСтруктураКомпании,
	|	ЕСТЬNULL(ПериодыРаботыСотрудников.СтруктураКомпании.Должность.Наименование, """") + ""
	|("" + ЕСТЬNULL(ПериодыРаботыСотрудников.Сотрудник.ФизическоеЛицо.Наименование, """") + "")
	|"" + ЕСТЬNULL(""("" + ПериодыРаботыСотрудников.Сотрудник.Квалификация.Наименование + "")"", """") КАК ОтвественныйСотрудник
	|ИЗ
	|	ВТ КАК Самооценка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ КАК ОценкиОтветсвеных
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПериодыРаботыСотрудников КАК ПериодыРаботыСотрудников
	|			ПО ОценкиОтветсвеных.ДатаОценки <= ПериодыРаботыСотрудников.Уволен
	|				И ОценкиОтветсвеных.ДатаОценки >= ПериодыРаботыСотрудников.Принят
	|				И ОценкиОтветсвеных.Ответственный = ПериодыРаботыСотрудников.СтруктураКомпании
	|		ПО Самооценка.Стандарт = ОценкиОтветсвеных.Стандарт
	|ГДЕ
	|	Самооценка.Ответственный = &ОбъектУправления
	|	И НЕ ОценкиОтветсвеных.Ответственный = &ОбъектУправления";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ОбъектУправления",ОбъектУправления);
	Запрос.УстановитьПараметр("ПериодичностьПланирования",ПериодичностьПланирования);
	ГраницыПериода = KPI_ОбщегоНазначенияСервер.ГраницыПериода(Период,ПериодичностьПланирования);
	Запрос.УстановитьПараметр("НачалоПериода",ГраницыПериода.НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода" ,ГраницыПериода.КонецПериода);
	
	Выборка = Запрос.Выполнить().Выбрать();
	ОбластьГрупировкаСтандарты = МакетПланФакты.ПолучитьОбласть("ГрупировкаСтандарты");
	ДокументРезультат.Вывести(ОбластьГрупировкаСтандарты);
	ОбластьДетальныеЗаписиСтандарты = МакетПланФакты.ПолучитьОбласть("ДетальныеЗаписиСтандарты");
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ОбластьДетальныеЗаписиСтандарты.Параметры,Выборка);
		ДокументРезультат.Вывести(ОбластьДетальныеЗаписиСтандарты);
	КонецЦикла;
	
	//КонецСтандарты
	
	KPI_ИнтерфейсВызовСервера.ДобавитьПодписьСформированногоОтчета(ДокументРезультат,Период);
	
	
КонецПроцедуры
