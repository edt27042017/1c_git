
&НаКлиенте
Процедура ОтборФизЛицоПриИзменении(Элемент)
	
	ОтборыПриИзмененииСервер();
	
КонецПроцедуры

&НаСервере
Процедура ОтборыПриИзмененииСервер()
	
	ТекстУсловие = "";
	Если ЗначениеЗаполнено(ЭтаФорма.ОтборФизЛицо)
		И ЗначениеЗаполнено(ЭтаФорма.ОтборСтруктураКомпании) Тогда
		ТекстУсловие = " KPI_Сотрудники.ФизическоеЛицо = &ФизическоеЛицо
		| И KPI_Сотрудники.СтруктураКомпании = &СтруктураКомпании";
	ИначеЕсли ЗначениеЗаполнено(ЭтаФорма.ОтборФизЛицо) Тогда
		ТекстУсловие = " KPI_Сотрудники.ФизическоеЛицо = &ФизическоеЛицо";
	ИначеЕсли ЗначениеЗаполнено(ЭтаФорма.ОтборСтруктураКомпании) Тогда
		ТекстУсловие = " KPI_Сотрудники.СтруктураКомпании = &СтруктураКомпании";
	Иначе
		KPI_ИнтерфейсКлиентСервер.УдалитьЭлементОтбораСписка(ЭтаФорма.Список,
		"Ссылка");
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = "ВЫБРАТЬ
	|	KPI_СтруктураКомпании.СтруктураКомпании КАК СтруктураКомпании,
	|	KPI_СтруктураКомпании.Сотрудник КАК Сотрудник,
	|	KPI_СтруктураКомпании.Период
	|ПОМЕСТИТЬ СрезСтруктуры
	|ИЗ
	|	РегистрСведений.KPI_СтруктураКомпании КАК KPI_СтруктураКомпании
	|ГДЕ
	|	KPI_СтруктураКомпании.Сотрудник В(&Сотрудники)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СтруктураКомпании,
	|	Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	KPI_ПриказОНазначенииНаДолжностьСотрудники.Ссылка
	|ИЗ
	|	РегистрСведений.KPI_СтруктураКомпании.СрезПервых(
	|			,
	|			(СтруктураКомпании, Сотрудник) В
	|				(ВЫБРАТЬ
	|					СрезСтруктуры.СтруктураКомпании,
	|					СрезСтруктуры.Сотрудник
	|				ИЗ
	|					СрезСтруктуры КАК СрезСтруктуры)) КАК KPI_СтруктураКомпанииСрезПервых
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.KPI_ПриказОНазначенииНаДолжность.Сотрудники КАК KPI_ПриказОНазначенииНаДолжностьСотрудники
	|		ПО KPI_СтруктураКомпанииСрезПервых.СтруктураКомпании = KPI_ПриказОНазначенииНаДолжностьСотрудники.СтруктураКомпании
	|			И KPI_СтруктураКомпанииСрезПервых.Период = KPI_ПриказОНазначенииНаДолжностьСотрудники.Ссылка.Дата
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	KPI_ПриказОПереводеСотрудники.Ссылка
	|ИЗ
	|	СрезСтруктуры КАК СрезСтруктуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.KPI_ПриказОПереводе.Сотрудники КАК KPI_ПриказОПереводеСотрудники
	|		ПО СрезСтруктуры.СтруктураКомпании = KPI_ПриказОПереводеСотрудники.СтруктураКомпании
	|			И СрезСтруктуры.Период = KPI_ПриказОПереводеСотрудники.Ссылка.Дата
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	KPI_ПриказОСнятииСДолжностиСотрудники.Ссылка
	|ИЗ
	|	Документ.KPI_ПриказОСнятииСДолжности.Сотрудники КАК KPI_ПриказОСнятииСДолжностиСотрудники
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СрезСтруктуры КАК СрезСтруктуры
	|		ПО KPI_ПриказОСнятииСДолжностиСотрудники.Сотрудник = СрезСтруктуры.Сотрудник
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	KPI_ТабельОтработанноеВремя.Ссылка
	|ИЗ
	|	СрезСтруктуры КАК СрезСтруктуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.KPI_Табель.ОтработанноеВремя КАК KPI_ТабельОтработанноеВремя
	|		ПО СрезСтруктуры.Сотрудник = KPI_ТабельОтработанноеВремя.Сотрудник
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	KPI_Невыходы.Ссылка
	|ИЗ
	|	СрезСтруктуры КАК СрезСтруктуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.KPI_Невыходы КАК KPI_Невыходы
	|		ПО СрезСтруктуры.Сотрудник = KPI_Невыходы.Сотрудник
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	KPI_ВводПрекращениеНачислений.Ссылка
	|ИЗ
	|	СрезСтруктуры КАК СрезСтруктуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.KPI_ВводПрекращениеНачислений КАК KPI_ВводПрекращениеНачислений
	|		ПО СрезСтруктуры.Сотрудник = KPI_ВводПрекращениеНачислений.ОбъектУправления";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	
	ТекстЗапросаСотрудники = "ВЫБРАТЬ
	|	KPI_Сотрудники.Ссылка
	|ИЗ
	|	Справочник.KPI_Сотрудники КАК KPI_Сотрудники
	|ГДЕ
	|	&Условие";


	ТекстЗапросаСотрудники = СтрЗаменить(ТекстЗапросаСотрудники,"&Условие",ТекстУсловие);	
	ЗапросСотрудники = Новый Запрос(ТекстЗапросаСотрудники);
	
	ЗапросСотрудники.УстановитьПараметр("ФизическоеЛицо"   ,ЭтаФорма.ОтборФизЛицо);
	ЗапросСотрудники.УстановитьПараметр("СтруктураКомпании",ЭтаФорма.ОтборСтруктураКомпании);

	Сотрудники = ЗапросСотрудники.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	Запрос.УстановитьПараметр("Сотрудники",Сотрудники);
	
	ДокументыСотрудника = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	KPI_ИнтерфейсКлиентСервер.ИзменитьЭлементОтбораСписка(ЭтаФорма.Список,
	"Ссылка",ДокументыСотрудника,
	Истина,ВидСравненияКомпоновкиДанных.ВСписке);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборСтруктураКомпанииПриИзменении(Элемент)
	
	ОтборыПриИзмененииСервер();
	
КонецПроцедуры
