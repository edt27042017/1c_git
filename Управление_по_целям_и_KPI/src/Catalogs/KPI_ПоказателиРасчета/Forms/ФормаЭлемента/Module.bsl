
////////////////////////////////////////////////////////////////////////////////
// РАБОТА СО ЗНАЧЕНИЯМИ ПОКАЗАТЕЛЕЙ

&НаСервере
// Заполняет таблицу значений ЗначенияПоказателей данными о суммах, 
//   зафиксированных за сотрудниками по текущему показателю
//
// Параметры:
//	Нет.
//
Процедура ЗаполнитьТаблицуЗначенияПоказателей()
	
	Если Объект.Ссылка.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = "ВЫБРАТЬ
	|	KPI_ЗначенияПоказателей.Сотрудник,
	|	KPI_ЗначенияПоказателей.Значение
	|ИЗ
	|	РегистрСведений.KPI_ЗначенияПоказателей КАК KPI_ЗначенияПоказателей
	|ГДЕ
	|	KPI_ЗначенияПоказателей.Показатель = &Показатель";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Показатель", Объект.Ссылка);
	
	ЭтаФорма.ЗначенияПоказателей.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

&НаСервереБезКонтекста
// Выполняет запись данных о сумме, зафиксированной за сотрудником по показателю
//
// Параметры:
//	Нет.
//
Процедура ОтредактироватьЗначенияПоказателей(Показатель, Сотрудник, Значение, ТекущийСотрудник)
	
	// Проверка на изменение сотрудника
	Если НЕ Сотрудник = ТекущийСотрудник Тогда
		УдалитьЗначениеПоказателя(Показатель, ТекущийСотрудник);
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.KPI_ЗначенияПоказателей.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Показатель.Установить(Показатель);
	НаборЗаписей.Отбор.Сотрудник.Установить(Сотрудник);
	НаборЗаписей.Очистить();
	
	Запись             = НаборЗаписей.Добавить();
	Запись.Показатель  = Показатель;
	Запись.Сотрудник   = Сотрудник;
	Запись.Значение    = Значение;
	НаборЗаписей.Записать();
	
КонецПроцедуры

&НаСервереБезКонтекста
// Выполняет удаление данных о сумме, зафиксированной за сотрудником по показателю
//
// Параметры:
//	Нет.
//
Процедура УдалитьЗначениеПоказателя(Показатель, Сотрудник)
	
	НаборЗаписей = РегистрыСведений.KPI_ЗначенияПоказателей.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Показатель.Установить(Показатель);
	НаборЗаписей.Отбор.Сотрудник.Установить(Сотрудник);
	НаборЗаписей.Очистить();
    НаборЗаписей.Записать();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьТаблицуЗначенияПоказателей();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗначенияПоказателейПриИзменении(Элемент)
	
	Если Объект.Ссылка.Пустая() Тогда
		
		Если НЕ Вопрос("Для редактирования значений показателя необходимо записать показатель расчета. Записать?",
			РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да Тогда
			Возврат; 
		КонецЕсли;
		
		ЭтаФорма.Записать();
	КонецЕсли;
	
	ТекущиеДанные = Элементы.ЗначенияПоказателей.ТекущиеДанные;
	
	Если НЕ ТекущиеДанные = Неопределено И ЗначениеЗаполнено(ТекущиеДанные.Сотрудник) Тогда
		ОтредактироватьЗначенияПоказателей(Объект.Ссылка, ТекущиеДанные.Сотрудник, ТекущиеДанные.Значение, ЭтаФорма.ТекущийСотрудник);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗначенияПоказателейПередУдалением(Элемент, Отказ)
	
	ТекущиеДанные = Элементы.ЗначенияПоказателей.ТекущиеДанные;
	
	Если НЕ ТекущиеДанные = Неопределено И ЗначениеЗаполнено(ТекущиеДанные.Сотрудник) Тогда
		УдалитьЗначениеПоказателя(Объект.Ссылка, ТекущиеДанные.Сотрудник);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗначенияПоказателейСотрудникНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.ЗначенияПоказателей.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЭтаФорма.ТекущийСотрудник = ТекущиеДанные.Сотрудник;
	
	ОбъектУправления = KPI_ОбщегоНазначения.ОбработкаВыбораПолучитьСтруктуруКомпанииНаДату(ТекущаяДата(), 
							Элемент, СтандартнаяОбработка, Истина, ТекущиеДанные.Сотрудник);
	
	Если Не ОбъектУправления = Неопределено Тогда
		ПредставлениеОбъектаУправления = ОбъектУправления.СтруктураКомпанииПредставление;
		ТекущиеДанные.Сотрудник        = ОбъектУправления.Сотрудник;
		
		ЗначенияПоказателейПриИзменении(Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗначенияПоказателейПередНачаломИзменения(Элемент, Отказ)
	
	ЭтаФорма.ТекущийСотрудник = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗначенияПоказателейСотрудникОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Элементы.ЗначенияПоказателей.ТекущиеДанные.Сотрудник <> ВыбранноеЗначение.Сотрудник Тогда
	
		Отбор = Новый Структура;
		Отбор.Вставить("Сотрудник", ВыбранноеЗначение.Сотрудник);
		
		Если ЗначенияПоказателей.НайтиСтроки(Отбор).Количество() Тогда
			Сообщить(СокрЛП(ВыбранноеЗначение.Сотрудник)+" уже присутствует в списке!");
			ВыбранноеЗначение.Сотрудник = Неопределено;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры


