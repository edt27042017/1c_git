&НаКлиенте
Процедура ОК(Команда)
	Если Не ЗначениеЗаполнено(НаименованиеФильтраАналитик) Тогда
		Сообщить("Не заполнено название фильтра аналитик");
		Возврат;
	КонецЕсли;
	
	//проверим на заполнение значения хотя бы для одной аналитики
	АналитикаЗаполнена = Ложь;
	Для НомерАналитики = 1 по 5 Цикл
		Если ЗначениеЗаполнено(ЭтаФорма["Значение" + НомерАналитики]) Тогда
			АналитикаЗаполнена = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если Не АналитикаЗаполнена Тогда
		Сообщить("Не выбрано ни одного значения аналитики");
		Возврат;
	КонецЕсли;
	
	НайденныйКлючАналитики = НайтиКлючАналитики(НаименованиеФильтраАналитик,ЭтаФорма.Значение1, ЭтаФорма.Значение2, ЭтаФорма.Значение3, ЭтаФорма.Значение4, ЭтаФорма.Значение5);
	
	ФильтрАналитики = Новый Структура;
	ФильтрАналитики.Вставить("НаименованиеФильтраАналитик", НаименованиеФильтраАналитик);
	ФильтрАналитики.Вставить("КлючАналитики"              , НайденныйКлючАналитики);
	
	ОповеститьОВыборе(ФильтрАналитики);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НайтиКлючАналитики(НаименованиеКлюча,Аналитика1, Аналитика2, Аналитика3, Аналитика4, Аналитика5)
	
	КлючАналитики = KPI_ИнтерфейсВызовСервера.КлючАналитики(Аналитика1, Аналитика2,
															Аналитика3, Аналитика4, Аналитика5); 
															
	КлючАналитикиОбъект = КлючАналитики.ПолучитьОбъект();
	КлючАналитикиОбъект.Наименование = НаименованиеКлюча;
	КлючАналитикиОбъект.Записать();
	
	КлючАналитики                    = КлючАналитикиОбъект.Ссылка;
	Возврат КлючАналитики;
	
КонецФункции

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	//массив для описания типов
	Массив = Новый Массив;
	
	ЦП = Параметры.ЦП;
	
	Если Не KPI_ОбщегоНазначенияСервер.ЕстьАналитикиЦП(ЦП) Тогда
		Сообщить("Для целевого показателя """ + ЦП + """ не установлено ни одной аналитики");
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если НЕ Параметры.Свойство("КлючАналитики", ЭтаФорма.КлючАналитики) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	НаименованиеФильтраАналитик = Параметры.НаименованиеФильтраАналитик;
	
	Если Не ЗначениеЗаполнено(НаименованиеФильтраАналитик) Тогда
		НаименованиеФильтраАналитик = КлючАналитики.Наименование;
	КонецЕсли;
	
	Для НомерАналитики = 1 по 5 Цикл
		Если Не ПустаяСтрока(ЦП["ТипАналитики" + НомерАналитики + "Представление"]) Тогда
			ЭтаФорма["НазваниеАналитики" + НомерАналитики]                   = ЦП["НазваниеАналитики" + НомерАналитики];
			ЭтаФорма["ТипАналитики"      + НомерАналитики + "Представление"] = ЦП["ТипАналитики"      + НомерАналитики + "Представление"];
			
			Массив.Очистить();
			Массив.Добавить(Тип("СправочникСсылка." + ЭтаФорма["ТипАналитики" + НомерАналитики + "Представление"]));
			Элементы["Значение" + НомерАналитики].ОграничениеТипа = Новый ОписаниеТипов(Массив);
			
			РазрезАналитики = ЭтаФорма.КлючАналитики["Аналитика" + НомерАналитики];
			
			Если ЗначениеЗаполнено(РазрезАналитики) Тогда
				ЭтаФорма["Значение" + НомерАналитики] = РазрезАналитики;
			Иначе 
				ЭтаФорма["Значение" + НомерАналитики] = Неопределено;
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
	
	Для НомерАналитики = 1 по 5 Цикл
		Если Не ЗначениеЗаполнено(ЭтаФорма["ТипАналитики" + НомерАналитики + "Представление"]) Тогда
			Элементы["НазваниеАналитики" + НомерАналитики].Видимость = Ложь;
			Элементы["Значение" + НомерАналитики].Видимость = Ложь;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры






