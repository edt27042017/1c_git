//Получает ссылку на документ планирования за указанный период
//Параметры:
// Дата                      - тип дата, дата из периода, за которой надо получить документ
// ПериодичностьПланирования - тип ПеречислениеСсылка.KPI_ПериодичностьПланирования
// СтруктураКомпании         - СправочникСсылка.KPI_СтруктураКомпании
//Возвращаемое значение:
// тип ДокументСсылка.KPI_Планирование
Функция ПолучитьДокПланирование(Дата, ПериодичностьПланирования, СтруктураКомпании) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ
	|	KPI_Планирование.Ссылка
	|ИЗ
	|	Документ.KPI_Планирование КАК KPI_Планирование
	|ГДЕ
	|	KPI_Планирование.Дата = &Дата
	|	И KPI_Планирование.СтруктураКомпании = &СтруктураКомпании
	|	И KPI_Планирование.ПериодичностьПланирования = &ПериодичностьПланирования
	|	И KPI_Планирование.Проведен
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	KPI_Планирование.Ссылка
	|ИЗ
	|	Документ.KPI_Планирование КАК KPI_Планирование
	|ГДЕ
	|	KPI_Планирование.Дата = &Дата
	|	И KPI_Планирование.СтруктураКомпании = &СтруктураКомпании
	|	И KPI_Планирование.ПериодичностьПланирования = &ПериодичностьПланирования
	|	И НЕ KPI_Планирование.Проведен
	|	И НЕ KPI_Планирование.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("Дата"                     , KPI_ОбщегоНазначенияСервер.ГраницыПериода(Дата, ПериодичностьПланирования).НачалоПериода);
	Запрос.УстановитьПараметр("ПериодичностьПланирования", ПериодичностьПланирования);
	Запрос.УстановитьПараметр("СтруктураКомпании"        , СтруктураКомпании);
	
	Результат = Запрос.Выполнить();
	Выборка   = Результат.Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

//Функция получает список планов на период
//Параметры:
// Дата                      - тип дата, дата из периода, за которой надо получить документ
// ПериодичностьПланирования - тип ПеречислениеСсылка.KPI_ПериодичностьПланирования
// СтруктураКомпании         - СправочникСсылка.KPI_СтруктураКомпании
//Возвращаемое значение:
// тип массив, элемент массива СправочникСсылка.KPI_ЦелевыеПоказатели
Функция ПланыОбъектаУправленияНаПериод(Дата, ПериодичностьПланирования, СтруктураКомпании) Экспорт
	
	ТекстЗапроса = "ВЫБРАТЬ
	|	KPI_Планы.ЦелевойПоказатель
	|ИЗ
	|	РегистрНакопления.KPI_Планы КАК KPI_Планы
	|ГДЕ
	|	KPI_Планы.СтруктураКомпании = &СтруктураКомпании
	|	И KPI_Планы.ПериодичностьПланирования = &ПериодичностьПланирования
	|	И KPI_Планы.Период = &Период";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Период"                   , KPI_ОбщегоНазначенияСервер.ГраницыПериода(Дата, ПериодичностьПланирования).НачалоПериода);
	Запрос.УстановитьПараметр("ПериодичностьПланирования", ПериодичностьПланирования);
	Запрос.УстановитьПараметр("СтруктураКомпании"        , СтруктураКомпании);
	
	Результат = Запрос.Выполнить();
	
	Возврат Результат.Выгрузить().ВыгрузитьКолонку("ЦелевойПоказатель");
	
КонецФункции
