Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.KPI_ПлановыеНачисления.Записать();
	Движения.KPI_ПлановыеНачисления.Записывать       = Истина;
	
	Движения.KPI_ГрафикиРаботыСотрудников.Записать();
	Движения.KPI_ГрафикиРаботыСотрудников.Записывать = Истина;
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	KPI_ВводПрекращениеНачисленийНачисленияСотрудника.ВидНачисления КАК ВидНачисления,
	               |	KPI_ВводПрекращениеНачисленийНачисленияСотрудника.СуммаНачисления,
	               |	KPI_ВводПрекращениеНачисленийНачисленияСотрудника.ДатаНачала,
	               |	KPI_ВводПрекращениеНачисленийНачисленияСотрудника.ДатаОкончания
	               |ПОМЕСТИТЬ ИзмененыеНачисленияСотрудника
	               |ИЗ
	               |	Документ.KPI_ВводПрекращениеНачислений.НачисленияСотрудника КАК KPI_ВводПрекращениеНачисленийНачисленияСотрудника
	               |ГДЕ
	               |	KPI_ВводПрекращениеНачисленийНачисленияСотрудника.Ссылка = &Ссылка
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	ВидНачисления
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ИзмененыеНачисленияСотрудника.ВидНачисления КАК ИзмененоеНачисление,
	               |	ИзмененыеНачисленияСотрудника.СуммаНачисления КАК ИзмененыйРазмерНачисления,
	               |	ИзмененыеНачисленияСотрудника.ДатаОкончания КАК ДатаОкончания,
	               |	МАКСИМУМ(ВЫБОР
	               |			КОГДА KPI_ПлановыеНачисленияСрезПоследних.Начисление ЕСТЬ NULL 
	               |				ТОГДА ИСТИНА
	               |			ИНАЧЕ ЛОЖЬ
	               |		КОНЕЦ) КАК НачислениеДобавлено,
	               |	МАКСИМУМ(ВЫБОР
	               |			КОГДА НЕ(ИзмененыеНачисленияСотрудника.ВидНачисления = KPI_ПлановыеНачисленияСрезПоследних.Начисление
	               |						И ИзмененыеНачисленияСотрудника.СуммаНачисления = KPI_ПлановыеНачисленияСрезПоследних.Сумма
	               |						И ИзмененыеНачисленияСотрудника.ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1))
	               |				ТОГДА ИСТИНА
	               |			ИНАЧЕ ЛОЖЬ
	               |		КОНЕЦ) КАК НачислениеИзменено
	               |ИЗ
	               |	ИзмененыеНачисленияСотрудника КАК ИзмененыеНачисленияСотрудника
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.KPI_ПлановыеНачисления.СрезПоследних(&Период, Сотрудник = &ОбъектУправления) КАК KPI_ПлановыеНачисленияСрезПоследних
	               |		ПО ИзмененыеНачисленияСотрудника.ВидНачисления = KPI_ПлановыеНачисленияСрезПоследних.Начисление
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ИзмененыеНачисленияСотрудника.ВидНачисления,
	               |	ИзмененыеНачисленияСотрудника.СуммаНачисления,
	               |	ИзмененыеНачисленияСотрудника.ДатаОкончания";
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Ссылка"           , Ссылка);
	Запрос.УстановитьПараметр("Период"           , Дата);
	Запрос.УстановитьПараметр("ОбъектУправления" , ОбъектУправления);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ОтборСотрудник = Новый Структура("Сотрудник",ОбъектУправления);
	ТекущийГрафик  = РегистрыСведений.KPI_ГрафикиРаботыСотрудников.ПолучитьПоследнее(Дата,ОтборСотрудник).ГрафикРаботы;
	Если НЕ ТекущийГрафик = ГрафикРаботы Тогда 
		Движение              = Движения.KPI_ГрафикиРаботыСотрудников.Добавить();
		Движение.Сотрудник    = ОбъектУправления;
		Движение.Период       = Дата;
		Движение.ГрафикРаботы = ГрафикРаботы;
	КонецЕсли;
	
	Пока Выборка.Следующий() Цикл
		
		Если Отказ Тогда 
			Продолжить;
		КонецЕсли;
		
		Если Выборка.НачислениеДобавлено Тогда
			Если Не ЗначениеЗаполнено(ДатаНачалаНовыхНачислений) Тогда
				KPI_ИнтерфейсКлиентСервер.СообщитьОбОшибке("Ошибка! Не указано начало периода новых начислений.");
				Отказ = Истина;
				Возврат;
			КонецЕсли;
			Движение = Движения.KPI_ПлановыеНачисления.Добавить();
			Движение.Период        = ДатаНачалаНовыхНачислений;
			Движение.Сотрудник     = ОбъектУправления;
			Движение.Начисление    = Выборка.ИзмененоеНачисление;
			Движение.Сумма         = Выборка.ИзмененыйРазмерНачисления;
			Движение.ДатаНачала    = ДатаНачалаНовыхНачислений;
			Движение.ДатаОкончания = Выборка.ДатаОкончания;
			
			
		ИначеЕсли Выборка.НачислениеИзменено Тогда
			Движение = Движения.KPI_ПлановыеНачисления.Добавить();
			Движение.Период        = ?(ЗначениеЗаполнено(Выборка.ДатаОкончания),Выборка.ДатаОкончания,Дата);
			Движение.Сотрудник     = ОбъектУправления;
			Движение.Начисление    = Выборка.ИзмененоеНачисление;
			Движение.Сумма         = Выборка.ИзмененыйРазмерНачисления;
			Движение.ДатаНачала    = Движение.Период;
			//признак удаления начисления заполненая дата окончания
			Движение.ДатаОкончания = Выборка.ДатаОкончания;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры
