

Процедура ОбработкаПроведения(Отказ, Режим)
	// регистр KPI_Факты 
	Движения.KPI_Факты.Записывать = Истина;
	Движения.KPI_Факты.Записать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	KPI_ВводОценокСтандартовОценки.Оценка,
	|	KPI_ВводОценокСтандартовОценки.НомерСтроки,
	|	KPI_ВводОценокСтандартовОценки.Стандарт КАК Стандарт,
	|	KPI_ВводОценокСтандартовОценки.ДатаОценки,
	|	KPI_ВводОценокСтандартовОценки.Комментарий
	|ПОМЕСТИТЬ ВводОценок
	|ИЗ
	|	Документ.KPI_ВводОценокСтандартов.Оценки КАК KPI_ВводОценокСтандартовОценки
	|ГДЕ
	|	KPI_ВводОценокСтандартовОценки.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Стандарт
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(KPI_СтандартыСрезПоследних.ВесСтандарта, 0) КАК ВесСтандарта,
	|	ЕСТЬNULL(KPI_СтандартыСрезПоследних.ВесОтветственного, 0) КАК ВесОтветственного,
	|	ВводОценок.Оценка,
	|	ВводОценок.НомерСтроки,
	|	ВводОценок.Стандарт,
	|	ВводОценок.ДатаОценки,
	|	ВводОценок.Комментарий
	|ИЗ
	|	ВводОценок КАК ВводОценок
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.KPI_Стандарты.СрезПоследних(
	|				,
	|				ПериодичностьПланирования = &ПериодичностьПланирования
	|					И Оцениваемый = &Оцениваемый
	|					И Ответственный = &Ответственный
	|					И Основание ССЫЛКА Документ.KPI_Планирование) КАК KPI_СтандартыСрезПоследних
	|		ПО ВводОценок.Стандарт = KPI_СтандартыСрезПоследних.Стандарт
	|			И ВводОценок.ДатаОценки = KPI_СтандартыСрезПоследних.ДатаОценки";
	
	ГраницыПериода = KPI_ОбщегоНазначенияСервер.ГраницыПериода(Дата, ПериодичностьПланирования);
	
	Запрос.УстановитьПараметр("Ссылка"                   , Ссылка);
	Запрос.УстановитьПараметр("Оцениваемый"              , СтруктураКомпании);
	Запрос.УстановитьПараметр("Период"                   , ГраницыПериода.КонецПериода);
	Запрос.УстановитьПараметр("ПериодичностьПланирования", ПериодичностьПланирования);
	Запрос.УстановитьПараметр("Ответственный"            , АвторДокументаСтруктураКомпании);
	Результат              = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = Результат.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Движение = Движения.KPI_Факты.Добавить();
		
		Если Не ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.Оценка) Тогда
			Сообщить("Не выбрана оценка в строке №" + ВыборкаДетальныеЗаписи.НомерСтроки);
			Отказ = Истина;
		Иначе 
			//Если Не ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.Оценка.ЧисленноеЗначение) Тогда
			//	Сообщить("Не заполнено значение для оценки """ + ВыборкаДетальныеЗаписи.Оценка.Наименование + """ в строке №" + ВыборкаДетальныеЗаписи.НомерСтроки);
			//	Отказ = Истина;
			//Иначе 
			Движение.Факт = ВыборкаДетальныеЗаписи.ВесСтандарта * ВыборкаДетальныеЗаписи.ВесОтветственного * ВыборкаДетальныеЗаписи.Оценка.ЧисленноеЗначение / 10000;
			//КонецЕсли; 
		КонецЕсли;
		
		Аналитика1      = ВыборкаДетальныеЗаписи.Стандарт;
		Аналитика2      = ПериодичностьПланирования;
		Аналитика3      = АвторДокументаСтруктураКомпании; //ответственный
		Аналитика4      = СтруктураКомпании; //оцениваемый
		Аналитика5      = ВыборкаДетальныеЗаписи.ДатаОценки;
		
		КлючАналитики   = KPI_ИнтерфейсВызовСервера.КлючАналитики(Аналитика1,Аналитика2,Аналитика3,Аналитика4,Аналитика5);
		
		Движение.Период            = Дата;
		Движение.ЦелевойПоказатель = Справочники.KPI_ЦелевыеПоказатели.Стандарт;
		Движение.СтруктураКомпании = СтруктураКомпании;
		
		Движение.КлючАналитики     = КлючАналитики;
		Движение.ОценкаСтандарта   = ВыборкаДетальныеЗаписи.Оценка;
		Движение.Комментарий       = ВыборкаДетальныеЗаписи.Комментарий;
		
	КонецЦикла;
	
	
	Если НЕ Отказ Тогда
		//Изменяем записи в РС Стандарты
		Для каждого стр Из Оценки Цикл
			Запись                           = РегистрыСведений.KPI_Стандарты.СоздатьМенеджерЗаписи();
			Запись.Основание                 = Ссылка;
			Запись.Период                    = ?(стр.ДатаОценки = Дата(1, 1, 1), Дата, стр.ДатаОценки);
			Запись.ДатаОценки                = ?(стр.ДатаОценки = Дата(1, 1, 1), Дата, стр.ДатаОценки);
			Запись.Стандарт                  = стр.Стандарт;
			Запись.Ответственный             = АвторДокументаСтруктураКомпании;
			Запись.ВесСтандарта              = стр.ВесСтандарта;
			Запись.ВесОтветственного         = стр.ВесОтветственного;
			Запись.Оцениваемый               = СтруктураКомпании;
			Запись.ПериодичностьПланирования = ПериодичностьПланирования;
			Запись.Записать();
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры


Процедура ОбработкаУдаленияПроведения(Отказ)
	
	//Очищаем набор записей по текущему документу по РС Стандарты
	Набор = РегистрыСведений.KPI_Стандарты.СоздатьНаборЗаписей();
	Набор.Отбор.Основание.Установить(Ссылка);
	Набор.Прочитать();
	Набор.Очистить();
	Набор.Записать();
	
КонецПроцедуры


Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения)= Тип("Структура") Тогда
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект,ДанныеЗаполнения);
		
	КонецЕсли;
	
КонецПроцедуры


Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если Не ЗначениеЗаполнено(АвторДокументаСотрудник) Тогда
		АвторДокументаСотрудник = ПараметрыСеанса.KPI_ТекущийСотрудник;
	КонецЕсли;
	
	ТекстЗапроса = "ВЫБРАТЬ
	|	KPI_СтандартыСрезПоследних.ДатаОценки,
	|	KPI_СтандартыСрезПоследних.Стандарт,
	|	KPI_СтандартыСрезПоследних.Основание
	|ИЗ
	|	РегистрСведений.KPI_Стандарты.СрезПоследних(
	|			,
	|			Ответственный = &Ответсвенный
	|				И Оцениваемый = &Оцениваемый
	|				И ДатаОценки В (&ДатыОценок)
	|				И Стандарт В (&Стандарты)
	|				И Основание ССЫЛКА Документ.KPI_ВводОценокСтандартов) КАК KPI_СтандартыСрезПоследних
	|ГДЕ
	|	НЕ KPI_СтандартыСрезПоследних.Основание = &Основание";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Оцениваемый" ,СтруктураКомпании);
	Запрос.УстановитьПараметр("Ответсвенный",АвторДокументаСтруктураКомпании);
	Запрос.УстановитьПараметр("ДатыОценок"  ,Оценки.ВыгрузитьКолонку("ДатаОценки"));
	Запрос.УстановитьПараметр("Стандарты"   ,Оценки.ВыгрузитьКолонку("Стандарт"));
	Запрос.УстановитьПараметр("Основание"   ,Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Отказ = Истина;
		Сообщение = "Ошибка! Для стандарта """+ Выборка.Стандарт + """ оценка за " + Формат(Выборка.ДатаОценки,"ДФ=dd.MM.yyyy") + "
		|введена документом " + Выборка.Основание; 
		KPI_ИнтерфейсКлиентСервер.СообщитьОбОшибке(Сообщение);
	КонецЦикла;
КонецПроцедуры



