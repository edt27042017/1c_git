
&НаКлиенте
Процедура Заполнить(Команда)
	
	Если Объект.Проведен Тогда
		Ответ = Вопрос("Перед заполнением необходимо распровести документ, выполнить отмену проведения ?", РежимДиалогаВопрос.ДаНет);
		
		Если Ответ = КодВозвратаДиалога.Да Тогда
			Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.ОтменаПроведения));
		Иначе
			Возврат;
		КонецЕсли;	
	КонецЕсли;	
	
	
	Если Не ЗначениеЗаполнено(Объект.Дата) Тогда 
		Сообщить("Не введена дата документа");
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.ПериодичностьПланирования) Тогда 
		Сообщить("Не введена периодичность планирования");
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.СтруктураКомпании) Тогда 
		Сообщить("Не введен объект управления");
		Возврат;
	КонецЕсли;
	
	ЗаполнитьНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНаСервере()
	
	ТекстЗапроса = "ВЫБРАТЬ
	|	БезОценок.Основание,
	|	БезОценок.ДатаОценки КАК ДатаОценки,
	|	БезОценок.Стандарт,
	|	БезОценок.ВесСтандарта,
	|	БезОценок.ВесОтветственного,
	|	Одинаковые.Колво
	|ИЗ
	|	(ВЫБРАТЬ
	|		Планирование.Основание КАК Основание,
	|		Планирование.ДатаОценки КАК ДатаОценки,
	|		Планирование.Стандарт КАК Стандарт,
	|		Планирование.Оцениваемый КАК Оцениваемый,
	|		Планирование.ВесСтандарта КАК ВесСтандарта,
	|		Планирование.ВесОтветственного КАК ВесОтветственного
	|	ИЗ
	|		(ВЫБРАТЬ
	|			KPI_Стандарты.Основание КАК Основание,
	|			KPI_Стандарты.ДатаОценки КАК ДатаОценки,
	|			KPI_Стандарты.Стандарт КАК Стандарт,
	|			KPI_Стандарты.Оцениваемый КАК Оцениваемый,
	|			KPI_Стандарты.ВесСтандарта КАК ВесСтандарта,
	|			KPI_Стандарты.ВесОтветственного КАК ВесОтветственного
	|		ИЗ
	|			РегистрСведений.KPI_Стандарты КАК KPI_Стандарты
	|		ГДЕ
	|			KPI_Стандарты.Основание ССЫЛКА Документ.KPI_Планирование) КАК Планирование
	|			ПОЛНОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|				KPI_Стандарты.Основание КАК Основание,
	|				KPI_Стандарты.ДатаОценки КАК ДатаОценки,
	|				KPI_Стандарты.Стандарт КАК Стандарт,
	|				KPI_Стандарты.Оцениваемый КАК Оцениваемый,
	|				KPI_Стандарты.ВесСтандарта КАК ВесСтандарта,
	|				KPI_Стандарты.ВесОтветственного КАК ВесОтветственного
	|			ИЗ
	|				РегистрСведений.KPI_Стандарты КАК KPI_Стандарты
	|			ГДЕ
	|				KPI_Стандарты.Основание ССЫЛКА Документ.KPI_ВводОценокСтандартов) КАК ВводОценокСтандартов
	|			ПО Планирование.ДатаОценки = ВводОценокСтандартов.ДатаОценки
	|				И Планирование.Стандарт = ВводОценокСтандартов.Стандарт
	|				И Планирование.Оцениваемый = ВводОценокСтандартов.Оцениваемый
	|	ГДЕ
	|		ВводОценокСтандартов.Основание ЕСТЬ NULL ) КАК БезОценок
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			KPI_Стандарты.Стандарт КАК Стандарт,
	|			KPI_Стандарты.Оцениваемый КАК Оцениваемый,
	|			КОЛИЧЕСТВО(KPI_Стандарты.ДатаОценки) КАК Колво
	|		ИЗ
	|			РегистрСведений.KPI_Стандарты КАК KPI_Стандарты
	|		
	|		СГРУППИРОВАТЬ ПО
	|			KPI_Стандарты.Стандарт,
	|			KPI_Стандарты.Оцениваемый) КАК Одинаковые
	|		ПО БезОценок.Оцениваемый = Одинаковые.Оцениваемый
	|			И БезОценок.Стандарт = Одинаковые.Стандарт
	|ГДЕ
	|	БезОценок.ДатаОценки >= &НачалоПериода
	|	И БезОценок.ДатаОценки <= &КонецПериода
	|	И БезОценок.Оцениваемый = &Оцениваемый"
	;
	
	
	СтруктураГраницПериода = Новый Структура;
	СтруктураГраницПериода = KPI_ОбщегоНазначенияСервер.ГраницыПериода(Объект.Дата, Объект.ПериодичностьПланирования);	
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("НачалоПериода", СтруктураГраницПериода.НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", СтруктураГраницПериода.КонецПериода);
	Запрос.УстановитьПараметр("Оцениваемый", Объект.СтруктураКомпании);
	
	Объект.Оценки.Загрузить(Запрос.Выполнить().Выгрузить());	
	
КонецПроцедуры

&НаКлиенте
Процедура СтруктураКомпанииПредставлениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбъектУправления = KPI_ОбщегоНазначения.ОбработкаВыбораПолучитьСтруктуруКомпанииНаДату(Объект.Дата, ЭтаФорма, СтандартнаяОбработка, , Объект.СтруктураКомпании);
	
	Если Не ОбъектУправления = Неопределено Тогда
		Объект.СтруктураКомпании = ОбъектУправления.СтруктураКомпании;
		СтруктураКомпанииПредставление = ОбъектУправления.СтруктураКомпанииПредставление;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СтруктураКомпанииПредставлениеОчистка(Элемент, СтандартнаяОбработка)
	Объект.СтруктураКомпании = Неопределено;
КонецПроцедуры

&НаКлиенте
Процедура ДатаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Объект.СтруктураКомпании) Тогда
		Сообщить("Перед изменением даты необходимо очистить поле ""Объект управления""");
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	Объект.Оценки.Очистить();
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодичностьПриИзменении(Элемент)
	
	Объект.Оценки.Очистить();
	
КонецПроцедуры

&НаКлиенте
Процедура СтруктураКомпанииПредставлениеПриИзменении(Элемент)
	Объект.Оценки.Очистить();
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПредставления()
	СтруктураКомпанииПредставление = KPI_ОбщегоНазначенияСервер.ПолучитьПредставлениеПоСтруктуреКомпанииНаДату(Объект.СтруктураКомпании, Объект.Дата);
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	ЗаполнитьПредставления();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ЗаполнитьПредставления();
КонецПроцедуры



