
&НаКлиенте
Процедура ОтработанноеВремяСотрудникНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПараметрыОткрытия    = Новый Структура("Филиал", Объект.Филиал);
	ФормаВыбора          = ОткрытьФорму("Справочник.KPI_Сотрудники.ФормаВыбора", ПараметрыОткрытия , Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтработанноеВремяСотрудникПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ОтработанноеВремя.ТекущиеДанные;
	ЗаполнитьОтработанноеВремяСотрудника(ТекущиеДанные.Сотрудник);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьОтработанноеВремяСотрудника(Сотрудник)
	
	ОтборСотрудника = Новый Структура("Сотрудник",Сотрудник);
	НайденныеСтроки = Объект.ОтработанноеВремя.НайтиСтроки(ОтборСотрудника);
	
	Если ЗначениеЗаполнено(НайденныеСтроки) Тогда
		ТекстВопроса = "Перезаполнить отработанные часы по " + Сотрудник + "?";
		Ответ = Вопрос(ТекстВопроса,РежимДиалогаВопрос.ДаНет);
		Если Ответ = КодВозвратаДиалога.Да Тогда
			Для Каждого Стр из НайденныеСтроки Цикл
				Объект.ОтработанноеВремя.Удалить(Стр);
			КонецЦикла;
		Иначе 
			Возврат;
		КонецЕсли;
	КонецЕсли;
	ЗаполнитьОтработанныеЧасы(Сотрудник);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОтработанныеЧасы(Сотрудник = Неопределено,Списком = Ложь)
	
	Если Списком Тогда 
		Объект.ОтработанноеВремя.Очистить();
	Иначе 
		ОтборСотрудника = Новый Структура("Сотрудник",Сотрудник);
		НайденныеСтроки = Объект.ОтработанноеВремя.НайтиСтроки(ОтборСотрудника);
		Для Каждого Стр из НайденныеСтроки Цикл
			Объект.ОтработанноеВремя.Удалить(Стр);
		КонецЦикла;
	КонецЕсли;
	
	ВыборкаСотрудников = Неопределено;
	
	НачалоПериода = НачалоМесяца(Объект.Дата);
	КонецПериода  = КонецМесяца(Объект.Дата);
	
	Если Списком Тогда
		ВыборкаСотрудников = KPI_ИнтерфейсСервер.РабочееВремяСотрудников(НачалоПериода,КонецПериода,Объект.Филиал);
	Иначе 
		ВыборкаСотрудников = KPI_ИнтерфейсСервер.РабочееВремяСотрудников(НачалоПериода,КонецПериода,Сотрудник);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ВыборкаСотрудников) Тогда
	    Возврат;
	КонецЕсли;
	
	Пока ВыборкаСотрудников.Следующий() Цикл
		Стр                         = Объект.ОтработанноеВремя.Добавить();
		Стр.Сотрудник               = ВыборкаСотрудников.Сотрудник;
		ВыборкаОтработанногоВремени = ВыборкаСотрудников.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаОтработанногоВремени.Следующий() Цикл
			Стр[ВыборкаОтработанногоВремени.РабочийДень] =  ВыборкаОтработанногоВремени.Часы;	 
		КонецЦикла;
	КонецЦикла;			
	
КонецПроцедуры

&НаКлиенте
Процедура ОтработанноеВремяЗаполнитьПодбором(Команда)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ЗакрыватьПриВыборе",Ложь);
	ДополнительныеПараметры.Вставить("Филиал",Объект.Филиал);
	KPI_ОбщегоНазначения.ОбработкаВыбораПолучитьСтруктуруКомпанииНаДату(Объект.Дата, Элементы.ОтработанноеВремя,,,,ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтработанноеВремяЗаполнитьСписком(Команда)
	
	Если ЗначениеЗаполнено(Объект.Филиал) Тогда
		ЗаполнитьОтработанныеЧасы(,Истина);
	Иначе 
		KPI_ИнтерфейсКлиентСервер.СообщитьОбОшибке("Ошибка! Не указан филиал.");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтработанноеВремяОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Структура") И ВыбранноеЗначение.Свойство("Сотрудник") Тогда
		 ЗаполнитьОтработанноеВремяСотрудника(ВыбранноеЗначение.Сотрудник);
	КонецЕсли;
	
КонецПроцедуры


